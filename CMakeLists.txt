CMAKE_MINIMUM_REQUIRED(VERSION 3.12.1)

PROJECT(SimpleSwitcher LANGUAGES CSharp)
SET(CMAKE_CSharp_FLAGS "/langversion:7")

# Option to set path for ATEM Switchers SDK
if (NOT DEFINED SWITCHERS_SDK_INCLUDE_DIR)
    SET(SWITCHERS_SDK_INCLUDE_DIR ${CMAKE_CURRENT_SOURCE_DIR})
endif ()

# Compile BMDSwitcherAPI IDL to build directory
SET(MIDL_OUTPUT_TLB "${CMAKE_CURRENT_BINARY_DIR}\\BMDSwitcherAPI.tlb")
SET(SWITCHER_API_INTEROP "${CMAKE_CURRENT_BINARY_DIR}\\Interop.BMDSwitcherAPI.dll")
SET(SWITCHER_API_IDL "${SWITCHERS_SDK_INCLUDE_DIR}\\BMDSwitcherAPI.idl")

IF (CMAKE_SYSTEM_PROCESSOR MATCHES "(x86)|(X86)")
    SET(CSHARP_PLATFORM x86)
    SET(MIDL_MACHINE win32)
ELSE ()
    SET(CSHARP_PLATFORM x64)
    SET(MIDL_MACHINE amd64)
ENDIF ()

# Compile the BMDSwitcherAPI.idl file
ADD_CUSTOM_COMMAND(
        OUTPUT ${MIDL_OUTPUT_TLB}
        DEPENDS ${SWITCHER_API_IDL}
        COMMAND midl /env ${MIDL_MACHINE} ${SWITCHER_API_IDL} /tlb ${MIDL_OUTPUT_TLB}
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
        )

SET_SOURCE_FILES_PROPERTIES(${MIDL_OUTPUT_TLB} PROPERTIES GENERATED TRUE)

ADD_CUSTOM_TARGET(MIDL_Target
        DEPENDS ${MIDL_OUTPUT_TLB}
        )

# Generate BMDSwitcherAPI interop
ADD_CUSTOM_COMMAND(
        OUTPUT ${SWITCHER_API_INTEROP}
        DEPENDS MIDL_Target
        COMMAND tlbimp ${MIDL_OUTPUT_TLB} /out:${SWITCHER_API_INTEROP} /machine:${CSHARP_PLATFORM} /namespace:BMDSwitcherAPI
        WORKING_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}
        )

SET_SOURCE_FILES_PROPERTIES(${SWITCHER_API_INTEROP} PROPERTIES GENERATED TRUE)

ADD_CUSTOM_TARGET(SwitchersAPI_Interop_Target
        DEPENDS ${SWITCHER_API_INTEROP}
        )

# Add C# projects
ADD_EXECUTABLE(SimpleSwitcher
        Program.cs
        )

ADD_DEPENDENCIES(SimpleSwitcher SwitchersAPI_Interop_Target)

TARGET_COMPILE_OPTIONS(SimpleSwitcher PRIVATE "/platform:${CSHARP_PLATFORM}")

SET_TARGET_PROPERTIES(SimpleSwitcher PROPERTIES 
        VS_DOTNET_TARGET_FRAMEWORK_VERSION "v4.6.1" 
        VS_DOTNET_REFERENCES "Microsoft.CSharp;System"
		VS_PACKAGE_REFERENCES "AutoMapper_10.1.1"
        VS_DOTNET_REFERENCE_BMDSwitcherAPI "${SWITCHER_API_INTEROP}"
        VS_DOTNET_REFERENCES_COPY_LOCAL TRUE
        )

